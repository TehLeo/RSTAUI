plugins {
    id 'com.github.spotbugs' version '2.0.1'
}
['java', 'checkstyle', 'com.github.spotbugs', 'distribution', 'maven-publish', 'signing'].each { apply plugin: it }

assert JavaVersion.current().isJava8Compatible()

group = 'com.fifesoft'
archivesBaseName = 'rstaui'

repositories {
    mavenCentral()
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots'
    }
}
dependencies {
    implementation files("${projectDir.absolutePath}/../RSyntaxTextArea/RSyntaxTextArea/build/classes/java/main")
    implementation files("${projectDir.absolutePath}/../RSyntaxTextArea/RSyntaxTextArea/build/resources/main")
    implementation files("${projectDir.absolutePath}/../AutoComplete/AutoComplete/build/classes/java/main")
    implementation files("${projectDir.absolutePath}/../AutoComplete/AutoComplete/build/resources/main")
    testImplementation 'junit:junit:4.12'
}

ext.isReleaseVersion = !project.version.endsWith('SNAPSHOT')

wrapper {
	gradleVersion = '6.0.1'
}

checkstyle {
    toolVersion = '8.26'
    configFile = 'config/checkstyle/checkstyle.xml' as File
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

compileJava {
	sourceCompatibility javaVersion
	targetCompatibility javaVersion
	options.debug = true
	options.debugOptions.debugLevel = 'source,vars,lines'
	options.compilerArgs << '-Xlint:deprecation' << '-Xlint:unchecked'
}

ext.sharedManifest = manifest {
	attributes('Class-Path': 'rsyntaxtextarea.jar autocomplete.jar',
		'Specification-Title': 'RSTAUI',
		'Specification-Version': version,
		'Implementation-Title': 'org.fife.ui',
		'Implementation-Version': version)

}

java {
    withSourcesJar()
    withJavadocJar()
}
jar {
	manifest { from sharedManifest }
}

// We use "distributions" to create the zip files uploaded to SourceForge
distributions {
	main {
        distributionBaseName = 'rstaui'
		contents {
			from { [ 'build/libs' ] }
			rename 'RSTAUI-.*\\.jar', 'rstaui.jar'
		}
	}
	src {
        distributionBaseName = 'rstaui'
	}
}
distZip.classifier = null
distZip.dependsOn jar
srcDistZip.classifier = 'src'
srcDistZip {
	from projectDir
	include 'src/**/*'
	include 'build.gradle'
	include '.classpath'
	include '.project'
	include 'gradle.properties'
	include 'gradle/**/*'
	include 'gradlew*'
	include 'README.md'
	include '.settings/**'
}

task buildSourceForgeZips {
    doLast {
        println 'Building zip files for SourceForge'
    }
}
buildSourceForgeZips.dependsOn clean, jar, distZip, srcDistZip

publishing {
    repositories {
        maven {
            def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
            url = isReleaseVersion ? releasesRepoUrl : snapshotsRepoUrl
            credentials { // Credentials usually kept in user's .gradle/gradle.properties
                // We must defensively check for these properties so Travis CI build works
                username = project.hasProperty('ossrhUsername') ? ossrhUsername : 'unknown'
                password = project.hasProperty('ossrhPassword') ? ossrhPassword : 'unknown'
            }
        }
    }
    publications {
        maven(MavenPublication) {

            groupId = 'com.fifesoft'
            artifactId = 'rstaui'
            version = version

            from components.java

            pom {
                name = 'rstaui'
                description = 'RSTAUI is an add-on library for RSyntaxTextArea that provides pre-build dialog boxes commonly needed in text editing applications.'
                url = 'http://www.fifesoft.com/rsyntaxtextarea/'
                inceptionYear = '2003'
                packaging = 'jar'
                licenses {
                    license {
                        name = 'BSD-3-Clause'
                        url = 'http://fifesoft.com/rsyntaxtextarea/RSyntaxTextArea.License.txt'
                    }
                }
                developers {
                    developer {
                        name = 'Robert Futrell'
                    }
                }
                scm {
                    url = 'https://github.com/bobbylight/RSTAUI'
                    connection = 'scm:git:git://github.com/bobbylight/RSTAUI'
                    developerConnection = 'scm:git:git@github.com:bobbylight/RSTAUI'
                    if (isReleaseVersion) {
                        tag = project.version
                    }
                }
            }
        }
    }
}

signing {
    // Don't require signing for e.g. ./gradlew install
    required { gradle.taskGraph.hasTask('publish') && isReleaseVersion }
    sign publishing.publications.maven
}
tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}
